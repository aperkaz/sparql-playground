#id:NXQ_00016
#title:Proteins with a mature chain of less than 1000 amino acids which are secreted and do not contain cysteines in the mature chain
#comment:The search takes into account all isoforms so sometimes you may see cysteines in a displayed isoform but at least one of the isoform does not contain any cysteine.
#tags:sequence,subcellular location,cellular component,processing
#acs:P10163,P01258,P01270
#count:165
#time:0.08

select distinct ?entry where {
  ?entry :isoform ?iso.
  ?iso :cellularComponent/:term cv:SL-0243;:sequence / :chain ?seq. #secreted
  ?iso :matureProtein [ :start ?mstart ; :end ?mend]
  filter ((?mend-?mstart > 0) && (?mend-?mstart < 1000))	 
  bind (?mend - ?mstart + 1 as ?mlen)	 
  bind (substr(?seq, ?mstart, ?mlen) as ?mseq)	 
  filter (!regex (?mseq,'C'))
}
